plugins {
    id 'java-library'
    id 'maven-publish'
    id 'org.jetbrains.kotlin.jvm' version '1.9.25'
}

group = 'gg.levely'
version = project.findProperty("version")

allprojects {
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: 'kotlin'

    repositories {
        mavenLocal()
        mavenCentral()

        maven {
            url = uri("https://maven.pkg.github.com/LevelyStudio/EventBus")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_USER")
                password = project.findProperty("gpr.token") ?: System.getenv("GITHUB_TOKEN")
            }
        }
    }
}

subprojects {

    version = rootProject.version
    group = rootProject.group

    def baseName = project.parent != rootProject
            ? "${rootProject.name}-${project.parent.name.capitalize()}-${project.name.capitalize()}"
            : "${rootProject.name}-${project.name.capitalize()}"

    archivesBaseName = baseName

    kotlin {
        jvmToolchain(8)
    }

    java {
        withSourcesJar()
        withJavadocJar()

        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

}

