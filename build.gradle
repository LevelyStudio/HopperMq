plugins {
    id 'java-library'
    id 'maven-publish'
    id 'org.jetbrains.kotlin.jvm' version '2.1.10'
}

group = 'gg.levely'
version = project.findProperty("version")

repositories {
    mavenCentral()

    maven {
        url = uri("https://maven.pkg.github.com/LevelyStudio/EventBus")
        credentials {
            username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_USER")
            password = project.findProperty("gpr.token") ?: System.getenv("GITHUB_TOKEN")
        }
    }
}

ext {
    kotlinVersion = "2.1.10"
    amqpClientVersion = "5.25.0"
    eventBusVersion = "1.0.0"
    reflectionsVersion = "0.10.2"
    logbackVersion = "1.3.15"
}

dependencies {
    api "com.rabbitmq:amqp-client:$amqpClientVersion"
    api "gg.levely.system:eventbus:$eventBusVersion"

    implementation "org.reflections:reflections:$reflectionsVersion"
    implementation "ch.qos.logback:logback-classic:$logbackVersion"
}

// Génération des artefacts sources et Javadoc
tasks.register("sourcesJar", Jar) {
    archiveClassifier.set("sources")
    from sourceSets.main.allSource
}

tasks.register("javadocJar", Jar) {
    archiveClassifier.set("javadoc")
    from javadoc
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = 'gg.levely.system'
            artifactId = 'hoppermq'

            from components.java

            artifact tasks.sourcesJar
            artifact tasks.javadocJar
        }
    }

    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/${System.getenv("GITHUB_REPOSITORY")}")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_ACTOR")
                password = project.findProperty("gpr.token") ?: System.getenv("GITHUB_TOKEN")
            }
        }
    }
}

test {
    useJUnitPlatform()
}

kotlin {
    jvmToolchain(8)
}
